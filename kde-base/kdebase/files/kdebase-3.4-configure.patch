diff -Nur kdebase-3.4.2.orig/kcontrol/input/configure.in.bot kdebase-3.4.2/kcontrol/input/configure.in.bot
--- kdebase-3.4.2.orig/kcontrol/input/configure.in.bot	2005-05-23 14:13:38.000000000 +0200
+++ kdebase-3.4.2/kcontrol/input/configure.in.bot	2005-07-28 13:35:17.000000000 +0200
@@ -1,10 +1,4 @@
-if test "$LUSBLIB" = "no"; then
-  echo
-  echo "   Problem!  libusb library not found, get it from"
-  echo "   http://libusb.sourceforge.net"
-fi
-
-if (test x"$USB_LIBS" = x""); then
+if test "x$with_libusb" = xcheck && test "x$have_libusb" = xno; then
   echo ""
   echo "Logitech mouse support is disabled. Provide libusb to enable it."
   echo ""
diff -Nur kdebase-3.4.2.orig/kcontrol/input/configure.in.in kdebase-3.4.2/kcontrol/input/configure.in.in
--- kdebase-3.4.2.orig/kcontrol/input/configure.in.in	2005-05-23 14:13:38.000000000 +0200
+++ kdebase-3.4.2/kcontrol/input/configure.in.in	2005-07-28 13:35:17.000000000 +0200
@@ -1,19 +1,26 @@
-LIBUSBCONFIG=""
-AC_PATH_PROG(LIBUSBCONFIG,libusb-config,no)
-if test $LIBUSBCONFIG = "no"  ; then
-    AC_MSG_WARN(Can't find libusb-config)
-    AC_MSG_WARN(Check your libusb installation)
-    USB_LIBS=""
-    USB_CFLAGS=""
-else  
-    USB_LIBS="$($LIBUSBCONFIG --libs)"
-    USB_CFLAGS="$($LIBUSBCONFIG --cflags)"
-AC_CHECK_LIB(usb,main,
-    AC_DEFINE(HAVE_LIBUSB, 1, [Defined if you have libusb]),
-    [],
-    $USB_LIBS
-)
+AC_ARG_WITH(libusb,
+    [AS_HELP_STRING(--with-libusb,
+        [enable control of some mouse models through libusb @<:@default=check@:>@])],
+    [], with_libusb=check)
+
+USB_LIBS=
+USB_CFLAGS=
+have_libusb=no
+if test "x$with_libusb" != xno; then
+    AC_PATH_PROG(LIBUSBCONFIG,libusb-config,no)
+    if test $LIBUSBCONFIG != "no"; then
+        USB_LIBS="$($LIBUSBCONFIG --libs)"
+        USB_CFLAGS="$($LIBUSBCONFIG --cflags)"
+    
+        AC_CHECK_LIB(usb,main,
+            [have_libusb=yes
+             AC_DEFINE(HAVE_LIBUSB, 1, [Defined if you have libusb])],
+            [], [$USB_LIBS]
+        )
+    fi
+    if test "x$with_libusb" != xcheck && test "x$have_libusb" = xno; then
+        AC_MSG_FAILURE([--with-libusb was given, but test for libusb failed])
+    fi
 fi
 AC_SUBST(USB_LIBS)
 AC_SUBST(USB_CFLAGS)
-
diff -Nur kdebase-3.4.2.orig/kcontrol/view1394/configure.in.in kdebase-3.4.2/kcontrol/view1394/configure.in.in
--- kdebase-3.4.2.orig/kcontrol/view1394/configure.in.in	2005-05-23 14:13:48.000000000 +0200
+++ kdebase-3.4.2/kcontrol/view1394/configure.in.in	2005-07-28 13:35:17.000000000 +0200
@@ -1,3 +1,15 @@
+AC_ARG_WITH(libraw1394,
+  [AS_HELP_STRING(--with-libraw1394,
+    [enable visualization of ieee1394 devices through libraw1394 @<:@default=check@:>@])],
+  [], with_libraw1394=check)
+
 VIEW1394_SUBDIR=
-KDE_CHECK_HEADER(libraw1394/raw1394.h, VIEW1394_SUBDIR=view1394)
+if test "x$with_libraw1394" != xno; then
+  KDE_CHECK_HEADER(libraw1394/raw1394.h, VIEW1394_SUBDIR=view1394)
+
+  if test "x$with_libraw1394" != xcheck && test -z "$VIEW1394_SUBDIR"; then
+    AC_MSG_FAILURE([--with-libraw1394 was given, but test for libraw1394 failed])
+  fi
+fi
+
 AM_CONDITIONAL(include_kcontrol_view1394, test -n "$VIEW1394_SUBDIR")
diff -Nur kdebase-3.4.2.orig/kioslave/smb/configure.in.bot kdebase-3.4.2/kioslave/smb/configure.in.bot
--- kdebase-3.4.2.orig/kioslave/smb/configure.in.bot	2005-05-23 14:14:19.000000000 +0200
+++ kdebase-3.4.2/kioslave/smb/configure.in.bot	2005-07-28 13:35:17.000000000 +0200
@@ -1,4 +1,4 @@
-if test "$have_libsmbclient" = no; then
+if test "x$with_samba" = xcheck && test "x$have_libsmbclient" = xno; then
   echo ""
   echo "You're missing libsmbclient from samba 3.0"
   echo "KDE will not be able to browse windows shares without it,"
diff -Nur kdebase-3.4.2.orig/kioslave/smb/configure.in.in kdebase-3.4.2/kioslave/smb/configure.in.in
--- kdebase-3.4.2.orig/kioslave/smb/configure.in.in	2005-05-23 14:14:19.000000000 +0200
+++ kdebase-3.4.2/kioslave/smb/configure.in.in	2005-07-28 13:35:17.000000000 +0200
@@ -1,20 +1,34 @@
 AC_DEFUN([SMB_CHECK],
 [
 AC_REQUIRE([KDE_CHECK_LARGEFILE])
-have_libsmbclient=yes
-KDE_CHECK_HEADER(libsmbclient.h, ,[have_libsmbclient=no] )
-KDE_CHECK_LIB(smbclient, smbc_new_context, [] ,[have_libsmbclient=no])
 
-AM_CONDITIONAL(include_kioslave_smb, test "$have_libsmbclient" = yes)
+AC_ARG_WITH(samba,
+  [AS_HELP_STRING(--with-samba,
+    [enable the samba ioslave @<:@default=check@:>@])],
+  [], with_samba=check)
 
-SMBCLIENT_EXTRA_LIBS=""
-AC_CHECK_FUNC(yp_get_default_domain, [],
+have_libsmbclient=no
+if test "x$with_samba" != xno; then
+  have_libsmbclient=yes
+  KDE_CHECK_HEADER(libsmbclient.h, [], [have_libsmbclient=no])
+  KDE_CHECK_LIB(smbclient, smbc_new_context, [], [have_libsmbclient=no])
+
+  SMBCLIENT_EXTRA_LIBS=""
+  AC_CHECK_FUNC(yp_get_default_domain, [],
   [
      KDE_CHECK_LIB(nsl, yp_get_default_domain, 
            [SMBCLIENT_EXTRA_LIBS="-lnsl"] )
   ])
 
-AC_SUBST(SMBCLIENT_EXTRA_LIBS)
+  AC_SUBST(SMBCLIENT_EXTRA_LIBS)
+
+  if test "x$with_samba" != xcheck && test "x$have_libsmbclient" = xno; then
+    AC_MSG_FAILURE([--with-samba was given, but test for libsmbclient failed])
+  fi
+fi
+
+AM_CONDITIONAL(include_kioslave_smb, test "x$have_libsmbclient" = xyes)
+
 ])
 
 SMB_CHECK
diff -Nur kdebase-3.4.2.orig/kioslave/thumbnail/configure.in.in kdebase-3.4.2/kioslave/thumbnail/configure.in.in
--- kdebase-3.4.2.orig/kioslave/thumbnail/configure.in.in	2005-05-23 14:14:20.000000000 +0200
+++ kdebase-3.4.2/kioslave/thumbnail/configure.in.in	2005-07-28 13:35:59.000000000 +0200
@@ -99,6 +99,18 @@
   AC_SUBST(EXR_FLAGS)
 ])
 
-AC_FIND_LIBEXR
+AC_ARG_WITH(openexr,
+  [AC_HELP_STRING(--with-openexr,
+    [enable support for OpenEXR @<:@default=check@:>@])],
+  [], with_openexr=check)
+
+if test "x$with_openexr" != xno; then
+  AC_FIND_LIBEXR
+
+  if test "x$with_openexr" != xcheck && test -z "$LIB_EXR"; then
+    AC_MSG_ERROR([--with-openexr was given, but test for OpenEXR failed])
+  fi
+fi
+
 AM_CONDITIONAL(include_EXR_MODULES, test -n "$LIB_EXR")
 
diff -Nur kdebase-3.4.2.orig/ksysguard/configure.in.in kdebase-3.4.2/ksysguard/configure.in.in
--- kdebase-3.4.2.orig/ksysguard/configure.in.in	2005-05-23 14:15:09.000000000 +0200
+++ kdebase-3.4.2/ksysguard/configure.in.in	2005-07-28 13:35:17.000000000 +0200
@@ -23,11 +23,19 @@
 AM_CONDITIONAL(include_ksysguardd_openbsd, test "$UNAME" = OpenBSD)
 AC_SUBST(UNAME)
 
-KDE_CHECK_LIB(sensors, sensors_init, [LIBSENSORS="-lsensors"])
-AC_SUBST(LIBSENSORS)
+AC_ARG_WITH(sensors,
+	[AS_HELP_STRING(--with-sensors,
+		[enable support for lm_sensors @<:@default=check@:>@])],
+	[], with_sensors=check)
 
-dnl Check for dell laptop support
-AM_CONDITIONAL(supports_i8k, test -f /proc/i8k)
+if test "x$with_sensors" != xno; then
+	KDE_CHECK_HEADERS(sensors/sensors.h)
+	KDE_CHECK_LIB(sensors, sensors_init, [LIBSENSORS="-lsensors"])
+
+	if test "x$with_readline" != xcheck && test -z "$LIBSENSORS"; then
+        	AC_MSG_FAILURE([--with-sensors was given, but test for lm_sensors failed])
+	fi
+fi
 
 case "$UNAME" in
 	Linux) LIBHOSTS="$LIBSENSORS" ;;
@@ -35,5 +43,7 @@
 	Solaris) LIBHOSTS="$LIBSENSORS -lkstat -lsocket" ;;
 esac
 AC_SUBST(LIBHOSTS)
+AC_SUBST(LIBSENSORS)
 
-KDE_CHECK_HEADERS(sensors/sensors.h)
+dnl Check for dell laptop support
+AM_CONDITIONAL(supports_i8k, test -f /proc/i8k)
